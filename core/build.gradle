/*
 * Copyright 2015 Francesco Pontillo
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

ext.name = 'crowd-pulse-core'
mainClassName = 'net.frakbot.crowdpulse.core.cli.Blade'
version = mainVersion
sourceCompatibility = 1.8

// ------ Common definitions ------- //

// Exclude files that match common regexps
ext.exclusion = { file ->
    def skip = [/.*\.properties/, /.*\.json/, /.*\.bin/, /.*\.sample/, /.*_index/, /sentiwordnet/,
     /stop-words-.*/, /morphit/, /tanl-morphit/]
    for (m in skip) {
        if (file.name.matches(m)) {
            return true
        }
    }
    return false
}
ext.sharedManifest = manifest {
    attributes("Class-Path": ".")
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
    exclude project.exclusion
}

shadowJar {
    baseName = project.name
    classifier = 'bundle'
    mergeServiceFiles()
    manifest = project.manifest {
        from sharedManifest
    }
    exclude project.exclusion
}

task execute(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath

    String argString = System.getProperty("args")
    if (argString) {
        args = Arrays.asList(argString.split(" "))
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.beust:jcommander:1.48'

    compile project(':common-util')
    compile project(':data-java')

    compile project(':social')
    compile project(':social-facebook')
    compile project(':social-twitter')

    compile project(':fix-geo-profile')
    compile project(':fix-geo-profile-google-maps')

    compile project(':fix-geo-message')
    compile project(':fix-geo-message-from-profile')

    compile project(':detect-language')
    compile project(':detect-language-optimaize')

    compile project(':tag')
    compile project(':tag-babelfy')
    compile project(':tag-me')
    compile project(':tag-open-calais')
    compile project(':tag-wikipedia-miner')

    compile project(':categorize')
    compile project(':categorize-wikipedia')

    compile project(':fix-geo-message')
    compile project(':fix-geo-message-from-profile')

    compile project(':tokenize')
    compile project(':tokenize-open-nlp')

    compile project(':rem-stop-word')
    compile project(':rem-stop-word-simple')

    compile project(':pos-tag')
    compile project(':pos-tag-open-nlp')

    compile project(':pos-tag-simple')
    compile project(':pos-tag-simple-multi')
    compile project(':pos-tag-simple-it')
    compile project(':pos-tag-simple-en')

    compile project(':lemmatize')
    compile project(':lemmatize-multi')
    compile project(':lemmatize-stanford-corenlp')
    compile project(':lemmatize-morphit')

    compile project(':sentiment')
    compile project(':sentiment-sentit')
    compile project(':sentiment-sentiwordnet')

    compile project(':infogram')

    compile project(':index-uniba')

    compile project(':email-notify')
}